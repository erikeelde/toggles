apply plugin: 'com.android.application'

android {

    dataBinding {
        enabled = true
    }

    compileSdkVersion Versions.compileSdk
    defaultConfig {
        applicationId "com.izettle.wrench"
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode 11
        versionName "1.0.10"

        testInstrumentationRunner "com.izettle.wrench.WrenchTestRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        def configurationAuthorityValue = applicationId + ".configprovider"
        def wrenchPermission = applicationId + ".permission"

        manifestPlaceholders = [configurationAuthority: configurationAuthorityValue, wrenchPermission: wrenchPermission]

        buildConfigField "String", "CONFIG_AUTHORITY", "\"${configurationAuthorityValue}\""

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-stetho.pro'
        }
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-stetho.pro'

            versionNameSuffix " debug"
            applicationIdSuffix = ".debug"
        }
    }
    lintOptions {
        baseline file("lint-baseline.xml")
        checkReleaseBuilds true
        abortOnError true
        warningsAsErrors true
        lintConfig file("../lint.xml")
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    testImplementation Libs.junit

    androidTestImplementation Libs.espresso.core
    androidTestImplementation Libs.androidTestingSupportLibrary.runner
    androidTestImplementation Libs.room.testing

    annotationProcessor Libs.lifecycle.compiler
    annotationProcessor Libs.room.compiler
    annotationProcessor Libs.dagger.daggerAndroidProcessor
    annotationProcessor Libs.dagger.daggerCompiler

    implementation Libs.support.app_compat
    implementation Libs.support.recyclerview
    implementation Libs.support.design
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation Libs.constraint_layout
    implementation Libs.support.cardview
    implementation Libs.lifecycle.extensions
    implementation Libs.room.runtime
    implementation Libs.paging

    implementation Libs.dagger.runtime
    implementation Libs.dagger.daggerAndroid
    implementation Libs.dagger.daggerAndroidSupport

    implementation Libs.wrench.core
    implementation Libs.wrench.prefs
}
